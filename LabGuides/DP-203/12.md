
# Exercise 02: Refactor the Bicep file

## Lab Scenario
You're participating in a training exercise focused on refactoring Bicep files for Azure resource deployment. In the previous exercise, you created an initial Bicep file for deploying a toy truck virtual machine and associated resources. However, the Bicep template does not adhere to best practices and lacks readability. Your task in this unit is to refactor the Bicep file to address these issues.

## Lab Objectives

In this lab, you'll perform the following tasks:

+ Task 1: Update the resource symbolic names
+ Task 2: Remove the redundant subnet resource
+ Task 3: Change the parameters to variables
+ Task 4: Update the resource locations
+ Task 5: Add parameters and variables
+ Task 6: Remove unnecessary properties
+ Task 7: Create a parameter file
+ Task 8: Verify your template

## Estimated Timing: 40 minutes

## Architecture Diagram

 ![](../../media/666.jpg)

## Task 1: Update the resource symbolic names

1. In Visual Studio Code, open the **main.bicep** file.

1. Select the symbolic name for the network security group resource, which is **`networkSecurityGroups_ToyTruckServer_nsg_name_resource`** or a similar name.

   >**Note:** Rename the symbolic name. You can select **F2** or right-click and then select **Rename Symbol**.
   >**Note:** Enter the name **`networkSecurityGroup`** and press **Enter**. Visual Studio Code updates the name and all references in the file.

1. Repeat this process for each resource. Rename the resources as shown in the following table.

   >**Note:** The names of the resources in your deployment will be a little different from the names in the table. Find the resources that have names that are close to these names.

    |Resource type|Current symbolic name|New symbolic name|
    |---|---|---|
    |Public IP address|	`publicIPAddresses_ToyTruckServer_ip_name_resource`|`publicIPAddress`|
    |Virtual machine|`virtualMachines_ToyTruckServer_name_resource`|`virtualMachine`|
    |Virtual network|`virtualNetworks_ToyTruck_vnet_name_resource`|`virtualNetwork`|
    |Subnet|`virtualNetworks_ToyTruck_vnet_name_default`|`defaultSubnet`|
    |Network interface|`networkInterfaces_toytruckserver890_name_resource`|`networkInterface`|

## Task 2: Remove the redundant subnet resource

The virtual network's subnet is currently defined twice. It's defined once in the **`virtualNetwork`** resource and again as its own child resource named **`defaultSubnet`**. It doesn't make sense to define the subnet twice.

1. **Delete** the **`defaultSubnet`** resource.

1. Notice that the **`networkInterface`** resource now displays a problem, because it refers to the default subnet's resource ID:

   ![](../../media/e20-t2-s2.png)

1. Update the **`virtualNetwork`** resource to include an `existing` reference to the subnet. If you add the `existing` reference, you can refer to the subnet again in your Bicep code without defining it again.

    ```Bicep
    resource virtualNetwork 'Microsoft.Network/virtualNetworks@2020-11-01' = {
      name: virtualNetworks_ToyTruck_vnet_name
      location: 'westus2'
      properties: {
        addressSpace: {
          addressPrefixes: [
            '10.0.0.0/16'
          ]
        }
        subnets: [
          {
            name: 'default'
            properties: {
              addressPrefix: '10.0.0.0/24'
              delegations: []
              privateEndpointNetworkPolicies: 'Enabled'
              privateLinkServiceNetworkPolicies: 'Enabled'
            }
          }
        ]
        virtualNetworkPeerings: []
        enableDdosProtection: false
      }
    
      resource defaultSubnet 'subnets' existing = {
        name: 'default'
      }
    }
    ```

1. Update the **`networkInterface`** resource to refer to the subnet's resource ID:

    ```Bicep
    resource networkInterface 'Microsoft.Network/networkInterfaces@2022-05-01' = {
      name: networkInterfaces_toytruckserver890_name
      location: 'westus2'
      properties: {
        ipConfigurations: [
          {
            name: 'ipconfig1'
            properties: {
              privateIPAddress: '10.0.0.4'
              privateIPAllocationMethod: 'Dynamic'
              publicIPAddress: {
                id: publicIPAddress.id
              }
              subnet: {
                id: virtualNetwork::defaultSubnet.id
              }
              primary: true
              privateIPAddressVersion: 'IPv4'
            }
          }
        ]
        dnsSettings: {
          dnsServers: []
        }
        enableAcceleratedNetworking: true
        enableIPForwarding: false
        disableTcpStateTracking: false
        networkSecurityGroup: {
          id: networkSecurityGroup.id
        }
        nicType: 'Standard'
      }
    }
    ```

    >**Note:** You'll notice an error about the expression being involved in the cycle. You'll fix that in the next step.
    

## Task 3: Change the parameters to variables

The parameters in the template don't need to be parameters. You'll now rename the parameters to more meaningful names and convert them to variables.

1. Select the symbolic name for the `virtualNetworks_ToyTruck_vnet_name` parameter. Rename it to `virtualNetworkName`.

1. Change the parameter to a variable. Remember to remove the type because variable definitions don't include types.

    ```Bicep
    var virtualNetworkName = 'ToyTruck-vnet'
    ```

1. Repeat the process for each parameter. Rename the parameters as shown in the following table.

   >**Note:** Notice that the value of the `networkInterfaceName` includes a three-digit number. The number is different for different deployments. Ensure that you copy the variable's value from your reference template.

   | Resource type | Current symbolic name |
   | --- | --- | 
   | `virtualMachines_ToyTruckServer_name` | `virtualMachineName` | 
   | `networkInterfaces_toytruckserver890_name` | `networkInterfaceName` | 
   | `publicIPAddresses_ToyTruckServer_ip_name` | `publicIPAddressName` | 
   | `networkSecurityGroups_ToyTruckServer_nsg_name` | `networkSecurityGroupName` | 

1. Verify that your variable declarations look like the following example:

    ```Bicep
    var virtualNetworkName = 'ToyTruck-vnet'
    var virtualMachineName = 'ToyTruckServer'
    var networkInterfaceName = 'YOUR-NETWORK-INTERFACE-NAME'
    var publicIPAddressName = 'ToyTruckServer-ip'
    var networkSecurityGroupName = 'ToyTruckServer-nsg'
    ```

   >**Note:** Rename all the variable values. Otherwise, you'll face issues while deploying Bicep to Azure. Some of the resource names might already be present in the Resource Group.

## Task 4: Update the resource locations

All the resources currently use a hard-coded location. You'll now add a parameter so that the template becomes more reusable.

1. At the top of the file, add a new parameter and a description decorator to clarify the parameter's purpose:

    ```Bicep
    @description('The location where resources are deployed.')
    param location string = resourceGroup().location
    ```

2. Update each resource to use the `location` parameter instead of the hard-coded `westus2` location.

## Task 5: Add parameters and variables

Your template has some hard-coded values where parameters or variables would be more appropriate. You'll now add parameters for properties that might change between deployments and variables for values that won't.

1. At the top of the **main.bicep** file, below the location parameter, add the following parameters:

    ```Bicep
    @description('The name of the size of the virtual machine to deploy.')
    param virtualMachineSizeName string = 'Standard_D2s_v3'
    
    @description('The name of the storage account SKU to use for the virtual machine\'s managed disk.')
    param virtualMachineManagedDiskStorageAccountType string = 'Premium_LRS'
    
    @description('The administrator username for the virtual machine.')
    param virtualMachineAdminUsername string = 'toytruckadmin'
    
    @description('The administrator password for the virtual machine.')
    @secure()
    param virtualMachineAdminPassword string
    
    @description('The name of the SKU of the public IP address to deploy.')
    param publicIPAddressSkuName string = 'Standard'
    
    @description('The virtual network address range.')
    param virtualNetworkAddressPrefix string
    
    @description('The default subnet address range within the virtual network')
    param virtualNetworkDefaultSubnetAddressPrefix string
    ```

    >**Note:** Some of the parameters have default values and others don't. Later, you'll create a parameter file to set most of these values.

1. Add the following new variable declarations below the **`networkSecurityGroupName`** variable:

    ```Bicep
    var virtualNetworkDefaultSubnetName = 'default'
    var virtualMachineImageReference = {
      publisher: 'canonical'
      offer: '0001-com-ubuntu-server-focal'
      sku: '20_04-lts-gen2'
      version: 'latest'
    }
    ```

1. Add the following variable declaration. Replace the value with the OS disk name from your own reference template.

    ```Bicep
    var virtualMachineOSDiskName = 'YOUR-OS-DISK-NAME'
    ```

    >**Note:** The value of the **`virtualMachineOSDiskName`** is unique. The value is different for each deployment. Ensure that you copy the variable's value from your reference template.
    
    >**Disclaimer:** Ensure that you copy the correct values for the **`virtualMachineOSDiskName`** and **`networkInterfaceName`** variables. Otherwise, Azure won't detect that you're declaring existing resources and might try to create new resources.

1. Your variable declarations should now look like this example:

    ```Bicep
    var virtualNetworkName = 'ToyTruck-vnet'
    var virtualMachineName = 'ToyTruckServer'
    var networkInterfaceName = 'YOUR-NETWORK-INTERFACE-NAME'
    var publicIPAddressName = 'ToyTruckServer-ip'
    var networkSecurityGroupName = 'ToyTruckServer-nsg'
    var virtualNetworkDefaultSubnetName = 'default'
    var virtualMachineImageReference = {
      publisher: 'canonical'
      offer: '0001-com-ubuntu-server-focal'
      sku: '20_04-lts-gen2'
      version: 'latest'
    }
    var virtualMachineOSDiskName = 'YOUR-OS-DISK-NAME'
    ```

1. Update the **`publicIPAddress`** resource to refer to a parameter:

   | Property | Parameter |
   | --- | --- | 
   | `sku.name` | `publicIPAddressSkuName` | 

1. Update the **`virtualMachine`** resource to refer to the parameters and variables:

   | Property | Parameter or variable |
   | --- | --- | 
   | `hardwareProfile.vmSize` | `virtualMachineSizeName` |
   | `storageProfile.imageReference` | `virtualMachineImageReference` - Use the variable name to replace the object's values, including the curly braces. |
   | `storageProfile.osDisk.name` | `virtualMachineOSDiskName` |
   | `storageProfile.osDisk.managedDisk.storageAccountType` | `virtualMachineManagedDiskStorageAccountType` |
   | `osProfile.adminUsername` | `virtualMachineAdminUsername` |
   | `osProfile.adminPassword` | `virtualMachineAdminPassword` | 

1. Update the **`virtualNetwork`** resource to refer to the parameters and variables:

   | Property | Parameter or variable |
   | --- | --- | 
   | `addressSpace.addressPrefixes` | `	virtualNetworkAddressPrefix` |
   | `subnets.name` | `virtualNetworkDefaultSubnetName` |
   | `subnets.addressPrefix` | `virtualNetworkDefaultSubnetAddressPrefix` |

1. Update the **`virtualNetwork`** resource's nested resource `defaultSubnet`:

   | Property | Variable |
   | --- | --- | 
   | `name` | `virtualNetworkDefaultSubnetName` |

 
 >**Note:** Please navigate to the **Task-8** in the same exercise to see how the modified field should look like after updating the above property variables.

## Task 6: Remove unnecessary properties

The export process adds redundant properties to many resources. Use these steps to remove the unneeded properties.

1. In the **`networkSecurityGroup`** resource, remove **`properties`** because the **`securityRules`** property is empty.

1. In the **`publicIPAddress`** resource, remove the following properties:
     - `ipAddress` property because it's automatically set by Azure.
     - `ipTags` property because it's empty.

1. In the **`virtualMachine`** resource, remove the following properties:
     - `storageProfile.osDisk.managedDisk.id` property because Azure automatically determines this property when the virtual machine is deployed.
       >**Note:** If you don't remove this property, your template won't deploy correctly.
     - `storageProfile.dataDisks` property because it's empty.
     - `osProfile.secrets` property because it's empty.
     - `osProfile.requireGuestProvisionSignal` property because Azure sets this property automatically.

1. In the **`virtualNetwork`** resource, remove the following properties:
     - `delegations` and `virtualNetworkPeerings` properties because they're empty.
     - The line for `type: 'Microsoft.Network/virtualNetworks/subnets'`

1. In the **`networkInterface`** resource, remove the following properties:
     - The `kind` property.
     - From `ipConfigurations`: `id`, `etag`, `type`, and `privateIPAddress` because it's automatically set by Azure and the allocation method is _Dynamic_
     - From `ipConfigurations.properties`:
         - `provisioningState`
     - From `publicIPAddress`, `name`, `properties`, `type`, and `sku`
     - `dnsSettings` because the `dnsServers` property is empty.

When you work with your own templates, you need to determine whether there are any properties that should be removed, as you did here.  

In Visual Studio Code, the Bicep extension helps you set the minimum properties for a resource. When you add a space after the equal sign in the resource definition, Visual Studio Code prompts you to select the required properties:

  ![](../media/e20-t6-s5.png)

When you select required properties, Visual Studio Code populates the resource definition with the properties that are mandatory. You can refer to required-properties to determine whether the properties in your converted template all need to be present.

The Azure Quickstart Templates repository is also helpful for this task. Find a quickstart template that does approximately what you're trying to do, and look at the properties it sets on the resource.

## Task 7: Create a parameter file

Your parameters are currently defined as default values in your template. To make your template work well across environments, it's a good idea to create a parameter file and remove default values for parameters that need to change for each environment.

1. Create a new file named **main.parameters.production.json**.

1. Paste the following JSON into the **main.parameters.production.json** file:

    ```JSON
    {
      "$schema": "https://schema.management.azure.com/schemas/2019-04-01/deploymentParameters.json#",
      "contentVersion": "1.0.0.0",
      "parameters": {
        "virtualMachineSizeName": {
          "value": "Standard_D2s_v3"
        },
        "virtualMachineManagedDiskStorageAccountType": {
            "value": "Premium_LRS"
        },
        "virtualMachineAdminUsername": {
            "value": "toytruckadmin"
        },
        "virtualNetworkAddressPrefix": {
            "value": "YOUR-VIRTUAL-NETWORK-ADDRESS-PREFIX"
        },
        "virtualNetworkDefaultSubnetAddressPrefix": {
            "value": "YOUR-SUBNET-ADDRESS-PREFIX"
        }
      }
    }
    ```

1. Update the values for the **`virtualNetworkAddressPrefix`** and **`virtualNetworkDefaultSubnetAddressPrefix`** parameters to match the IP address ranges that are specified in your reference template's virtual network resource.

   >**Note:** For example, here's how the values are specified in a reference template. Your IP addresses might be different from the IP addresses that are used in this example:

    ```Bicep
    resource virtualNetworks_ToyTruck_vnet_name_resource 'Microsoft.Network/virtualNetworks@2022-05-01' = {
      name: virtualNetworks_ToyTruck_vnet_name
      location: 'westus2'
      properties: {
        addressSpace: {
          addressPrefixes: [
            '10.0.0.0/16'
          ]
        }
        subnets: [
          {
            name: 'default'
            id: virtualNetworks_ToyTruck_vnet_name_default.id
            properties: {
              addressPrefix: '10.0.0.0/24'
              delegations: []
              privateEndpointNetworkPolicies: 'Disabled'
              privateLinkServiceNetworkPolicies: 'Enabled'
            }
            type: 'Microsoft.Network/virtualNetworks/subnets'
          }
        ]
        virtualNetworkPeerings: []
        enableDdosProtection: false
      }
    }
    ```

1. Update your **main.bicep** file to remove the default values for the parameters you specified in the parameters file.
     - `virtualMachineSizeName`
     - `virtualMachineManagedDiskStorageAccountType`
     - `virtualMachineAdminUsername`

     >**Note:** Don't change the default values for the `location` and `publicIPAddressSkuName` parameters because those values are probably the same for all your environments.

## Task 8: Verify your template

1. At the end of the refactor phase, your main.bicep file should look similar to the following example:

    ```Bicep
    @description('The location where resources are deployed.')
    param location string = resourceGroup().location
    
    @description('The name of the size of the virtual machine to deploy.')
    param virtualMachineSizeName string
    
    @description('The name of the storage account SKU to use for the virtual machine\'s managed disk.')
    param virtualMachineManagedDiskStorageAccountType string
    
    @description('The administrator username for the virtual machine.')
    param virtualMachineAdminUsername string
    
    @description('The administrator password for the virtual machine.')
    @secure()
    param virtualMachineAdminPassword string
    
    @description('The name of the SKU of the public IP address to deploy.')
    param publicIPAddressSkuName string = 'Standard'
    
    @description('The virtual network address range.')
    param virtualNetworkAddressPrefix string
    
    @description('The default subnet address range within the virtual network')
    param virtualNetworkDefaultSubnetAddressPrefix string
    
    var virtualNetworkName = 'ToyTruck-vnet'
    var virtualMachineName = 'ToyTruckServer'
    var networkInterfaceName = 'YOUR-NETWORK-INTERFACE-NAME'
    var publicIPAddressName = 'ToyTruckServer-ip'
    var networkSecurityGroupName = 'ToyTruckServer-nsg'
    var virtualNetworkDefaultSubnetName = 'default'
    var virtualMachineImageReference = {
      publisher: 'canonical'
      offer: '0001-com-ubuntu-server-focal'
      sku: '20_04-lts-gen2'
      version: 'latest'
    }
    var virtualMachineOSDiskName = 'YOUR-OS-DISK-NAME'
    
    resource networkSecurityGroup 'Microsoft.Network/networkSecurityGroups@2022-05-01' = {
      name: networkSecurityGroupName
      location: location
    }
    
    resource publicIPAddress 'Microsoft.Network/publicIPAddresses@2022-05-01' = {
      name: publicIPAddressName
      location: location
      sku: {
        name: publicIPAddressSkuName
        tier: 'Regional'
      }
      properties: {
        publicIPAddressVersion: 'IPv4'
        publicIPAllocationMethod: 'Static'
        idleTimeoutInMinutes: 4
      }
    }
    
    resource virtualMachine 'Microsoft.Compute/virtualMachines@2022-08-01' = {
      name: virtualMachineName
      location: location
      properties: {
        hardwareProfile: {
          vmSize: virtualMachineSizeName
        }
        storageProfile: {
          imageReference: virtualMachineImageReference
          osDisk: {
            osType: 'Linux'
            name: virtualMachineOSDiskName
            createOption: 'FromImage'
            caching: 'ReadWrite'
            managedDisk: {
              storageAccountType: virtualMachineManagedDiskStorageAccountType
            }
            deleteOption: 'Delete'
            diskSizeGB: 30
          }
        }
        osProfile: {
          computerName: virtualMachineName
          adminUsername: virtualMachineAdminUsername
          adminPassword: virtualMachineAdminPassword
          linuxConfiguration: {
            disablePasswordAuthentication: false
            provisionVMAgent: true
            patchSettings: {
              patchMode: 'ImageDefault'
              assessmentMode: 'ImageDefault'
            }
            enableVMAgentPlatformUpdates: false
          }
          allowExtensionOperations: true
        }
        networkProfile: {
          networkInterfaces: [
            {
              id: networkInterface.id
              properties: {
                deleteOption: 'Detach'
              }
            }
          ]
        }
        diagnosticsProfile: {
          bootDiagnostics: {
            enabled: true
          }
        }
      }
    }
    
    resource virtualNetwork 'Microsoft.Network/virtualNetworks@2022-05-01' = {
      name: virtualNetworkName
      location: location
      properties: {
        addressSpace: {
          addressPrefixes: [
            virtualNetworkAddressPrefix
          ]
        }
        subnets: [
          {
            name: virtualNetworkDefaultSubnetName
            properties: {
              addressPrefix: virtualNetworkDefaultSubnetAddressPrefix
              privateEndpointNetworkPolicies: 'Disabled'
              privateLinkServiceNetworkPolicies: 'Enabled'
            }
          }
        ]
        enableDdosProtection: false
      }
    
      resource defaultSubnet 'subnets' existing = {
        name: virtualNetworkDefaultSubnetName
      }
    }
    
    resource networkInterface 'Microsoft.Network/networkInterfaces@2022-05-01' = {
      name: networkInterfaceName
      location: location
      properties: {
        ipConfigurations: [
          {
            name: 'ipconfig1'
            properties: {
              privateIPAllocationMethod: 'Dynamic'
              publicIPAddress: {
                id: publicIPAddress.id
              }
              subnet: {
                id: virtualNetwork::defaultSubnet.id
              }
              primary: true
              privateIPAddressVersion: 'IPv4'
            }
          }
        ]
        enableAcceleratedNetworking: true
        enableIPForwarding: false
        disableTcpStateTracking: false
        networkSecurityGroup: {
          id: networkSecurityGroup.id
        }
        nicType: 'Standard'
      }
    }
    ```

1. Your **main.parameters.production.json** file should look similar to the following file, although you might have different IP address ranges listed:

    ```JSON
    {
      "$schema": "https://schema.management.azure.com/schemas/2019-04-01/deploymentParameters.json#",
      "contentVersion": "1.0.0.0",
      "parameters": {
        "virtualMachineSizeName": {
          "value": "Standard_D2s_v3"
        },
        "virtualMachineManagedDiskStorageAccountType": {
            "value": "Premium_LRS"
        },
        "virtualMachineAdminUsername": {
            "value": "toytruckadmin"
        },
        "virtualNetworkAddressPrefix": {
            "value": "10.0.0.0/16"
        },
        "virtualNetworkDefaultSubnetAddressPrefix": {
            "value": "10.0.0.0/24"
        }
      }
    }
    ```

1. Select **View > Problems** to display the problems pane.

   >**Note:** No problems are indicated.
   
   >**Tip:** When you work with your own templates, you might make different choices about the properties to parameterize and other customizations. Throughout this module, we provide general guidance to help you get started, but you'll need to consider your own environment and how you want to reuse your templates when you decide how to refactor your own Bicep files.


## Review
In this exercise, you've achieved the following:

+ Updated the symbolic names of resources for clarity and consistency.
+ Removed redundant subnet resources to streamline the template.
+ Converted parameters to variables for enhanced flexibility.
+ Adjusted resource locations as required.
+ Added necessary parameters and variables to improve template customization.
+ Eliminated unnecessary properties for a cleaner template structure.
+ Generated a parameter file to facilitate parameterization during deployment.
+ Verified the correctness and completeness of your template.

### You've successfully completed this exercise. Please proceed with the next exercise.


<inject key="labvm Admin Username"></inject>
<br>
<inject key="labvm DNS Name" enableCopy="false"/>


**Code Block**

```
<inject key="region"></inject>
<inject key="labvm Admin Username"/>
```


 ```Bicep
    resource networkInterface 'Microsoft.Network/networkInterfaces@2022-05-01' = {
      name: networkInterfaces_toytruckserver890_name
      location: 'westus2'
      properties: {
        ipConfigurations: [
          {
            name: 'ipconfig1'
            properties: {
              privateIPAddress: '10.0.0.4'
              privateIPAllocationMethod: 'Dynamic'
              publicIPAddress: {
                id: publicIPAddress.id
              }
              subnet: {
                id: virtualNetwork::defaultSubnet.id
              }
              primary: true
              privateIPAddressVersion: 'IPv4'
            }
          }
        ]
        dnsSettings: {
          dnsServers: []
        }
        enableAcceleratedNetworking: true
        enableIPForwarding: false
        disableTcpStateTracking: false
        networkSecurityGroup: {
          id: networkSecurityGroup.id
        }
        name: <inject key="labvm Admin Username"/>
        upperValue:<inject key="azureaduseremail"/>
      }
    }
    ```

https://cloud.oracle.com/?tenant={TenantDomainName}&domain={AADDomain}&region={ResourceGroupRegion}
URL : <inject value="https://cloud.oracle.com/?tenant=" key="azureaduseremail" value="&domain=" key="labvm Admin Username" value="&region=" key="region" enableCopy="true"/>

Default Value : <inject key="azureaduseremail" defaultValue="example@example.com"/>
